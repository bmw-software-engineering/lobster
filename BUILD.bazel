load("@bazel_gazelle//:def.bzl", "gazelle")
load("@pip//:requirements.bzl", "all_whl_requirements")
load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_python_gazelle_plugin//manifest:defs.bzl", "gazelle_python_manifest")
load("@rules_python_gazelle_plugin//modules_mapping:def.bzl", "modules_mapping")

# Our gazelle target points to the python gazelle binary.
# This is the simple case where we only need one language supported.
# If you also had proto, go, or other gazelle-supported languages,
# you would also need a gazelle_binary rule.
# See https://github.com/bazelbuild/bazel-gazelle/blob/master/extend.rst#example
gazelle(
    name = "gazelle",
    gazelle = "@rules_python_gazelle_plugin//python:gazelle_binary",
)

# This rule fetches the metadata for python packages we depend on. That data is
# required for the gazelle_python_manifest rule to update our manifest file.
modules_mapping(
    name = "modules_map",
    wheels = all_whl_requirements,
)

# Gazelle python extension needs a manifest file mapping from
# an import to the installed package that provides it.
# This macro produces two targets:
# - //:gazelle_python_manifest.update can be used with `bazel run`
#   to recalculate the manifest
# - //:gazelle_python_manifest.test is a test target ensuring that
#   the manifest doesn't need to be updated
gazelle_python_manifest(
    name = "gazelle_python_manifest",
    # include_stub_packages = True,
    modules_mapping = ":modules_map",
    # This is what we called our `pip_parse` rule, where third-party
    # python libraries are loaded in BUILD files.
    pip_repository_name = "pip",
    requirements = "requirements_lock_3_9.txt",
)

# This stanza calls a rule that generates targets for managing pip dependencies
# with pip-compile for a particular python version.
compile_pip_requirements(
    name = "requirements_3_9",
    timeout = "moderate",
    src = "requirements.in",
    requirements_txt = "requirements_lock_3_9.txt",
    requirements_windows = "requirements_windows_3_9.txt",
)

py_binary(
    name = "github_release",
    srcs = ["util/github_release.py"],
    visibility = ["//:__subpackages__"],
    deps = [
        ":lobster",
        "//lobster/common",
        "@pip//requests",
    ],
)

py_binary(
    name = "mkassets",
    srcs = ["util/mkassets.py"],
    visibility = ["//:__subpackages__"],
)

py_library(
    name = "lobster",
    srcs = [
        "packages/lobster-core/setup.py",
        "packages/lobster-metapackage/setup.py",
        "packages/lobster-monolithic/setup.py",
        "packages/lobster-tool-codebeamer/setup.py",
        "packages/lobster-tool-cpp/setup.py",
        "packages/lobster-tool-cpptest/setup.py",
        "packages/lobster-tool-gtest/setup.py",
        "packages/lobster-tool-json/setup.py",
        "packages/lobster-tool-python/setup.py",
        "packages/lobster-tool-trlc/setup.py",
        "tests_integration/projects/basic/nor.py",
        "util/bump_version_post_release.py",
        "util/changelog.py",
        "util/github_release.py",
        "util/mkassets.py",
        "util/release.py",
    ],
    visibility = ["//:__subpackages__"],
    deps = [
        "//lobster/common",
        "@pip//requests",
        "@pip//setuptools",
    ],
)
